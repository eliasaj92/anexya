plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.1'
    id 'jacoco'
    id 'checkstyle'
    id 'com.github.spotbugs' version '5.2.5'
    id 'com.diffplug.spotless' version '6.25.0'
}

group = 'com.anexya'
version = '0.0.1-SNAPSHOT'

def springBootVersion = '3.2.1'
def lombokVersion = '1.18.30'
def springdocVersion = '2.3.0'
def checkstyleVersion = '10.12.4'
def spotbugsVersion = '4.8.5'

def javaVersion = JavaVersion.VERSION_21
java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation platform("org.springframework.boot:spring-boot-dependencies:${springBootVersion}")
    testImplementation platform("org.springframework.boot:spring-boot-dependencies:${springBootVersion}")

    testImplementation sourceSets.main.output

    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-jdbc'
    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:${springdocVersion}"
    implementation 'software.amazon.awssdk:kms:2.25.39'

    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    runtimeOnly 'com.mysql:mysql-connector-j:8.2.0'

    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.testcontainers:mysql'
}

tasks.named('test') {
    useJUnitPlatform()
    finalizedBy tasks.jacocoTestReport
}

jacoco {
    toolVersion = "0.8.11"
}

checkstyle {
    toolVersion = checkstyleVersion
    configDirectory = file("${rootProject.rootDir}/config/checkstyle")
    configProperties = ["suppressionFile": file("${rootProject.rootDir}/config/checkstyle/suppressions.xml")]
}

tasks.withType(Checkstyle).configureEach {
    ignoreFailures = true
    reports {
        xml.required = false
        html.required = true
    }
}

spotbugs {
    toolVersion = spotbugsVersion
    effort = 'max'
    reportLevel = 'medium'
    excludeFilter = file("${rootProject.rootDir}/config/spotbugs/exclude.xml")
}

tasks.withType(com.github.spotbugs.snom.SpotBugsTask).configureEach {
    ignoreFailures = true
    excludeFilter = file("${rootProject.rootDir}/config/spotbugs/exclude.xml")
    reports {
        xml.required = false
        html.required = true
    }
}

spotless {
    java {
        target 'src/*/java/**/*.java'
        importOrder('java', 'javax', 'org', 'com', '')
        removeUnusedImports()
        eclipse('4.26').configFile("${rootProject.rootDir}/config/spotless/eclipse-format.xml")
        trimTrailingWhitespace()
        endWithNewline()
    }

    format('misc') {
        target '*.gradle', 'src/**/*.gradle', 'README.md'
        trimTrailingWhitespace()
        endWithNewline()
    }
}

def jacocoExcludes = [
        '**/AnexyaApplication*',
    '**/domain/**',
    '**/api/**',
    '**/config/**',
        '**/web/**',
        '**/repository/**',
        '**/cloud/**',
        '**/crypto/**',
        '**/service/*AggregationStrategy*'
]

tasks.jacocoTestReport {
    dependsOn tasks.test
    classDirectories.setFrom(files(classDirectories.files.collect { fileTree(dir: it, exclude: jacocoExcludes) }))
    reports {
        xml.required = true
        csv.required = false
        html.required = true
    }
}

tasks.named('jacocoTestCoverageVerification') {
    dependsOn tasks.test
    classDirectories.setFrom(files(classDirectories.files.collect { fileTree(dir: it, exclude: jacocoExcludes) }))
    violationRules {
        rule {
            element = 'BUNDLE'
            limit {
                counter = 'BRANCH'
                value = 'COVEREDRATIO'
                minimum = 0.95
            }
        }
    }
}

tasks.check {
    dependsOn tasks.jacocoTestCoverageVerification
    dependsOn tasks.checkstyleMain
    dependsOn tasks.checkstyleTest
    dependsOn tasks.spotbugsMain
    dependsOn tasks.spotbugsTest
}
